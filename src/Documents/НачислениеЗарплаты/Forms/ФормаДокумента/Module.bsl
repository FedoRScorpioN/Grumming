


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗаполнитьАвтоматически(Команда)
	ЗаполнитьАвтоматическиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьАвтоматическиНаСервере()
	Объект.Начисления.Очистить();
	
	ДатаНачала = НачалоМесяца(Объект.ПериодНачисления);
	ДатаОкончания = КонецМесяца(Объект.ПериодНачисления);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КИС_НачалоПериода.Период < &ДатаНачала
	|			ТОГДА &ДатаНачала
	|		ИНАЧЕ КИС_НачалоПериода.Период
	|	КОНЕЦ КАК Период,
	|	КИС_НачалоПериода.Сотрудник КАК Сотрудник,
	|	КИС_НачалоПериода.ГрафикРаботы КАК ГрафикРаботы,
	|	КИС_НачалоПериода.Оклад КАК Оклад,
	|	КИС_НачалоПериода.Работает КАК Работает
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&ДатаНачала, ) КАК КИС_НачалоПериода
	|ГДЕ
	|	КИС_НачалоПериода.Работает = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КИС.Период,
	|	КИС.Сотрудник,
	|	КИС.ГрафикРаботы,
	|	КИС.Оклад,
	|	КИС.Работает
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КИС
	|ГДЕ
	|	КИС.Период >= &ДатаНачала
	|	И КИС.Период <= &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.Сотрудник КАК Сотрудник,
	|	ВТ_Данные.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТ_Данные.Оклад КАК Оклад,
	|	ВТ_Данные.Период КАК ДатаНачала,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА СледующаяЗапись.Период <= &ДатаОкончания
	|				ТОГДА ВЫБОР
	|						КОГДА СледующаяЗапись.Работает = ИСТИНА
	|							ТОГДА ДОБАВИТЬКДАТЕ(СледующаяЗапись.Период, ДЕНЬ, -1)
	|						ИНАЧЕ СледующаяЗапись.Период
	|					КОНЕЦ
	|			ИНАЧЕ &ДатаОкончания
	|		КОНЕЦ) КАК ДатаОкончания
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Данные КАК СледующаяЗапись
	|		ПО ВТ_Данные.Сотрудник = СледующаяЗапись.Сотрудник
	|			И ВТ_Данные.Период < СледующаяЗапись.Период
	|ГДЕ
	|	ВТ_Данные.Работает = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Данные.Сотрудник,
	|	ВТ_Данные.Оклад,
	|	ВТ_Данные.Период,
	|	ВТ_Данные.ГрафикРаботы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Начисления.Добавить();
		НоваяСтрока.Сотрудник = Выборка.Сотрудник;
		НоваяСтрока.ГрафикРаботы = Выборка.ГрафикРаботы;
		//В ТЗ не указано, что ВидРасчета заполняется, поэтому его пропускаем
		НоваяСтрока.ДатаНачала = Выборка.ДатаНачала;
		НоваяСтрока.ДатаОкончания = Выборка.ДатаОкончания;
		НоваяСтрока.ПоказательРасчета = Выборка.Оклад;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
