#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Услуги.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
		СтрокаТЧ.СтатьяЗатрат = ПолучитьСтатьюЗатратПоНоменклатуре(СтрокаТЧ.Номенклатура);
		СтрокаТЧ.Цена = РаботаСЦенами.ПолучитьЦенуПокупкиНаДату(СтрокаТЧ.Номенклатура, Объект.Поставщик, Объект.Дата);
	Иначе
		СтрокаТЧ.Цена = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Услуги.ТекущиеДанные;
	РаботаСтабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Услуги.ТекущиеДанные;
	РаботаСтабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьЦены(Команда)
	Для Каждого СтрокаТЧ Из Объект.Услуги Цикл
		СтрокаТЧ.Цена = РаботаСЦенами.ПолучитьЦенуПокупкиНаДату(СтрокаТЧ.Номенклатура, Объект.Поставщик, Объект.Дата);
		РаботаСТабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСтатьюЗатратПоНоменклатуре(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	возврат Выборка.СтатьяЗатрат;
КонецФункции

#КонецОбласти